buildscript {
    ext {
        kotlinVersion = '1.2.61'
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: "kotlin-jpa"
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

version = "1.0.0"

def defaultEncoding = 'UTF-8'
def jvmVersion = '1.8'

sourceCompatibility = jvmVersion
targetCompatibility = jvmVersion

javadoc {
    options.encoding = defaultEncoding
    options.addBooleanOption('Xdoclint:none', true)
}

compileKotlin.dependsOn(processResources)

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

test {
    include "**/model/**", "**/controller/**", "**/usecase/**", "**/util/**", "**/context/**"
}

springBoot {
    buildInfo()
}

repositories {
    jcenter()
}

dependencies {
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-aop"
    compile "org.springframework.boot:spring-boot-starter-cache"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.ehcache:ehcache"
    compile "javax.cache:cache-api"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5"
    compile "commons-io:commons-io:2.6"
    compile "org.apache.commons:commons-lang3"
    compile "com.ibm.icu:icu4j:61.1"
    compile fileTree(dir: 'libs', includes: ['*.jar'])
    runtime "com.h2database:h2"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

eclipse {
    project {
        buildCommand 'org.jetbrains.kotlin.ui.kotlinBuilder'
        natures 'org.jetbrains.kotlin.core.kotlinNature'
    }
    classpath.containers 'org.jetbrains.kotlin.core.KOTLIN_CONTAINER'
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.8"
}
